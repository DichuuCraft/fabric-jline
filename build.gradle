plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '8.0.0'
}

repositories {
	mavenCentral()
}

archivesBaseName = project.archives_base_name
version = project.minecraft_version+'-'+project.mod_version
group = project.maven_group

configurations {
	shade
	implementation.extendsFrom shade
}

shadowJar {
	from project.configurations.shade
	configurations = [
		project.configurations.shade
	]

	archiveClassifier = 'all-unremapped'
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	// modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.

	compileOnly "com.google.code.gson:gson:2.8.6"
	shade "org.jline:jline-reader:${project.jline_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

jar {
	from "LICENSE"
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

task remapShadowJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: shadowJar) {
	input = tasks.shadowJar.archiveFile
	archiveClassifier = 'all'
	addNestedDependencies.set(true)
}

defaultTasks 'remapShadowJar'
